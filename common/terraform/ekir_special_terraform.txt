# Terraform go brrr
@gaiaCost = 7500
@gaiaTime = 3600

@highestCost = 12500
@highestTime = 9000

@machineCost = 10000
@machineTime = 7200

@hiveCost = 10000
@hiveTime = 7200

@lClusterCost = 1000

terraform_link = {
	to = "pc_machine"
	from = "pc_relic"

	resources = {
		category = terraforming

		cost = {
			energy = @machineCost
		}
	}

	duration = @machineTime

	potential = {
		exists = owner
		has_ascension_perk = ap_machine_worlds
	}

	condition = {
		from = {
			OR = {
				is_owned_by = root
				NOT = { has_modifier = "holy_planet" }
			}
		}
	}

	ai_weight = {
		weight = 10
		modifier = {
			factor = 0
			from = {
				any_owned_species = {
					NOR = {
						has_trait = trait_machine_unit
						has_trait = trait_mechanical
					}
					is_sapient = yes
				}
			}
		}
	}
}
terraform_link = {
	to = "pc_hive"
	from = "pc_relic"

	resources = {
		category = terraforming

		cost = {
			energy = @hiveCost
		}
	}

	duration = @hiveTime

	potential = {
		exists = owner
		has_ascension_perk = ap_hive_worlds
	}

	condition = {
		from = {
			OR = {
				is_owned_by = root
				NOT = { has_modifier = "holy_planet" }
			}
		}
	}

	ai_weight = {
		weight = 10
		modifier = {
			factor = 0
			NOT = { has_origin = origin_necrophage }
			from = {
				any_owned_species = {
					NOT = { has_trait = trait_hive_mind }
					is_sapient = yes
				}
			}
		}
	}
}
terraform_link = {
	from = "pc_relic"
	to = "pc_gaia"

	resources = {
		category = terraforming

		cost = {
			energy = @gaiaCost
		}
	}

	duration = @gaiaTime

	potential = {
		OR = {
			from = { has_planet_flag = terraform_nucleus_transfixed }
			AND = {
				exists = owner
				has_ascension_perk = "ap_world_shaper"
			}
		}
	}

	ai_weight = {
		weight = 1
	}
}